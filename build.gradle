apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		jcenter()
	}
}

sourceCompatibility = 1.8

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

group = 'weave-asm'
archivesBaseName = project.name.toLowerCase()

repositories {
	mavenCentral()
	maven {
		name = 'fabric'
		url = 'http://maven.fabricmc.net/'
	}
	maven {
		name = 'modmuss'
		url = 'http://maven.modmuss50.me/'
	}
	maven {
		name "RX14 Repository"
		url 'http://mvn.rx14.co.uk/local/'
	}
}

dependencies {
	compile 'com.google.guava:guava:23.+'
	compile 'org.ow2.asm:asm:6.2.1'
	compile 'org.ow2.asm:asm-analysis:6.2.1'
	compile 'org.ow2.asm:asm-commons:6.2.1'
	compile 'org.ow2.asm:asm-tree:6.2.1'
	compile 'org.ow2.asm:asm-util:6.2.1'

	compile 'enigma-asm:enigma:0.12.0.+:lib'
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId 'weave-asm'
			artifactId project.archivesBaseName
			version project.version

			from components.java
		}
	}
	repositories {
		maven {
			url "http://mavenupload.modmuss50.me/"
			if (project.hasProperty('mavenPass')) {
				credentials {
					username 'buildslave'
					password project.getProperty('mavenPass')
				}
			}
		}
	}
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/license.gradle'

jar {
	manifest {
		attributes	'Implementation-Title': 'Weave',
				'Implementation-Version': version,
				'Main-Class': "net.fabricmc.weave.Main"
	}
}

task fatJar(type: Jar) {
    from {
		configurations.compile.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	manifest {
		attributes	'Implementation-Title': 'Weave',
					'Implementation-Version': version,
					'Main-Class': "net.fabricmc.weave.Main"
	}
	classifier = 'fat'
	with jar
}

artifacts {
   archives fatJar
}
